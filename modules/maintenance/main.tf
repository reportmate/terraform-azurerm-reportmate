terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.33"
    }
  }
}

resource "azurerm_container_app_job" "maintenance" {
  name                         = "${var.resource_prefix}-db-maintenance"
  container_app_environment_id = var.container_app_environment_id
  resource_group_name          = var.resource_group_name
  location                     = var.location

  replica_timeout_in_seconds = 1800 # 30 minutes max
  replica_retry_limit        = 1    # Don't retry on failure

  template {
    container {
      name   = "reportmate-db-maintenance"  # Match Azure CLI created name
      image  = "${var.acr_login_server}/reportmate-maintenance:latest"
      cpu    = 0.25
      memory = "0.5Gi"

      env {
        name  = "DB_HOST"
        value = var.db_host
      }

      env {
        name  = "DB_NAME"
        value = var.db_name
      }

      env {
        name  = "DB_USER"
        value = var.db_user
      }

      env {
        name        = "DB_PASS"
        secret_name = "db-password"
      }

      env {
        name  = "EVENT_RETENTION_DAYS"
        value = tostring(var.event_retention_days)
      }
    }
  }

  schedule_trigger_config {
    cron_expression          = var.schedule_cron
    parallelism              = 1
    replica_completion_count = 1
  }

  secret {
    name  = "db-password"
    value = var.db_password
  }

  registry {
    server               = var.acr_login_server
    username             = "reportmateacr"
    password_secret_name = "reportmateacrazurecrio-reportmateacr"  # Auto-generated by Azure CLI
  }

  lifecycle {
    ignore_changes = [
      container_app_environment_id,  # Ignore resource group casing differences  
      secret,                        # Ignore secret value changes
      tags                          # Job created without tags
    ]
  }

}
